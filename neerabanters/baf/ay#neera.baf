/* Neera-Edwin Conflict */
IF
	%BGT_VAR%
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	InParty("edwin")
	!Dead("edwin")
	!See([ENEMY]) 
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("AY#NeeraEdwinInParty","Global",1)
		StartDialogueNoSet(Player1)
END

/* Neera-Imoen Stars Talk */
/* Initiate timer - 5 days until checks start */
IF 
	%BGT_VAR%
	Global("AY#NeImStars","GLOBAL",0)
	InParty(Myself) 
	InParty("%IMOEN_DV%") 
	!Dead("%IMOEN_DV%")
THEN 
  RESPONSE #100
    SetGlobal("AY#NeImStars","GLOBAL",1)
    SetGlobalTimer("AY#NeImStarsTimer","GLOBAL",FIVE_DAYS)
END

/* If conditions are right, 25% chance to fire dialog, set weather to clear */
IF 
	%BGT_VAR%
	Global("AY#NeImStars","GLOBAL",1)
	GlobalTimerExpired("AY#NeImStarsTimer","GLOBAL")
	InParty(Myself) 
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	InParty("%IMOEN_DV%") 
	See("%IMOEN_DV%") 
	!StateCheck("%IMOEN_DV%",CD_STATE_NOTVALID)
	CombatCounter(0) 
	!See([ENEMY]) 
	TimeOfDay(NIGHT)
	!AreaType(DUNGEON)
	!AreaType(CITY)
  	AreaType(OUTDOOR)
  	AreaType(WEATHER)
THEN 
  RESPONSE #25
    SetGlobal("AY#NeImStars","GLOBAL",2)
    Weather(NOWEATHER)
    SetGlobalTimer("AY#NeImStarsWeather","GLOBAL",10)
  RESPONSE #75
    SetGlobalTimer("AY#NeImStarsTimer","GLOBAL",1200)
END

/* Fire dialog */
IF 
	%BGT_VAR%
	Global("AY#NeImStars","GLOBAL",2)
	GlobalTimerExpired("AY#NeImStarsWeather","GLOBAL")
	InParty(Myself) 
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	InParty("%IMOEN_DV%") 
	See("%IMOEN_DV%") 
	!StateCheck("%IMOEN_DV%",CD_STATE_NOTVALID)
	CombatCounter(0) 
	!See([ENEMY]) 
	TimeOfDay(NIGHT)
	!AreaType(DUNGEON)
	!AreaType(CITY)
  	AreaType(OUTDOOR)
  	AreaType(WEATHER)
THEN
	RESPONSE #100
		SetGlobal("AY#NeImStars","GLOBAL",3)
		StartDialogueNoSet("%IMOEN_DV%")
END


/* Neera-Skie Rain Talk */
/* Initiate timer - 3 days until checks start */
IF 
	%BGT_VAR%
	Global("AY#NeSkRain","GLOBAL",0)
	InParty(Myself) 
	InParty("skie") 
	!Dead("skie")
THEN 
	RESPONSE #100
    	SetGlobal("AY#NeSkRain","GLOBAL",1)
    	SetGlobalTimer("AY#NeSkRainTimer","GLOBAL",THREE_DAYS)
END

/* If conditions are right, 25% chance of dialog firing - set weather to rain */
IF 
	%BGT_VAR%
	Global("AY#NeSkRain","GLOBAL",1)
	GlobalTimerExpired("AY#NeSkRainTimer","GLOBAL")
	InParty(Myself) 
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	InParty("skie") 
	See("skie") 
	!StateCheck("skie",CD_STATE_NOTVALID)
	CombatCounter(0) 
	!See([ENEMY]) 
	!AreaType(DUNGEON)
	!AreaType(CITY)
	AreaType(OUTDOOR)
	AreaType(WEATHER)
THEN 
	RESPONSE #25
    	SetGlobal("AY#NeSkRain","GLOBAL",2)
    	Weather(RAIN)
    	SetGlobalTimer("AY#NeSkRainWeather","GLOBAL",10)
  	RESPONSE #75
    	SetGlobalTimer("AY#NeSkRainTimer","GLOBAL",1200)
END

/* Fire dialog */
IF 
	%BGT_VAR%
	Global("AY#NeSkRain","GLOBAL",2)
	GlobalTimerExpired("AY#NeSkRainWeather","GLOBAL")
	InParty(Myself) 
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	InParty("skie") 
	See("skie") 
	!StateCheck("skie",CD_STATE_NOTVALID)
	CombatCounter(0) 
	!See([ENEMY]) 
	!AreaType(DUNGEON)
	!AreaType(CITY)
  	AreaType(OUTDOOR)
  	AreaType(WEATHER)
THEN
  	RESPONSE #100
    	SetGlobal("AY#NeSkRain","GLOBAL",3)
    	StartDialogueNoSet("skie")
END

/* Neera-Dynaheir Post Adoy-Red Wizard Battle Dialog */
IF 
	%BGT_VAR%
	Global("AdoyLeft","OH2010",3)
	Global("AY#NeDyRedWiz","GLOBAL",0)
	InParty(Myself) 
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	InParty("dynaheir") 
	AreaCheck("OH2010")
THEN
	RESPONSE #100
		SetGlobal("AY#NeDyRedWiz","GLOBAL",1)
END

IF 
	%BGT_VAR%
	Global("AdoyLeft","OH2010",3)
	Global("AY#NeDyRedWiz","GLOBAL",0)
	InParty(Myself) 
	!InParty("dynaheir") 
	AreaCheck("OH2010")
THEN
	RESPONSE #100
		SetGlobal("AY#NeDyRedWiz","GLOBAL",5)
END

IF 
	%BGT_VAR%
	Global("AY#NeDyRedWiz","GLOBAL",1)
	InParty(Myself) 
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	InParty("dynaheir") 
	See("dynaheir") 
	!StateCheck("dynaheir",CD_STATE_NOTVALID)
	CombatCounter(0) 
	!See([ENEMY]) 
	AreaCheck("OH2000")
THEN
	RESPONSE #100
		SetGlobal("AY#NeDyRedWiz","GLOBAL",2)
		StartDialogueNoSet("dynaheir")
END

/* Neera-Boo Dialog */
IF
	%BGT_VAR%
	Global("AY#NEERABOO","GLOBAL",1) 
	GlobalTimerExpired("AY#NEERABOOTIMER","GLOBAL")
	InParty(Myself) 
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	InParty("minsc") 
	See("minsc") 
	!StateCheck("minsc",CD_STATE_NOTVALID)
	CombatCounter(0) 
	!See([ENEMY]) 
THEN
	RESPONSE #100
		SetGlobal("AY#NEERABOO","GLOBAL",2)
    	StartDialogueNoSet("minsc")
END
